///////////////////////////////////////////////////////////////////////////////
//
// Build Script for building aggregated JavaPOS library from 
// JavaPOS Contracts, JavaPOS Controls, and JCL libraries
// 
// Author: denis.kuniss@dieboldnixdorf.com
// s
///////////////////////////////////////////////////////////////////////////////

plugins {
	id 'java-library'
	id 'signing'
	id 'eclipse'
    id 'maven-publish'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

ext.githubProjectUrl = 'https://github.com/JavaPOSWorkingGroup/javapos'

wrapper {
	gradleVersion = '7.1.1'
}


///////////////////////////////////////////////////////////////////////////////
// Names and Versions
///////////////////////////////////////////////////////////////////////////////

def artifactName = 'javapos'
group='org.javapos'
def uposVersion = '1.14'
version="${uposVersion}.3-SNAPSHOT" // the last part after dot is the build/release version

// dependency versions
def javaposContractsVersion = "$uposVersion.3"
def javaposControlsVersion  = "$uposVersion.1"
def jclVersion = '2.3.1'

sourceCompatibility = 8 // not really need, no compilation at all; but documentation reason

///////////////////////////////////////////////////////////////////////////////
// Build Dependencies
///////////////////////////////////////////////////////////////////////////////

repositories {
    mavenCentral()
    
    if (!System.getenv('CI') && project.hasProperty('localMavenRepo'))
    { // evaluates only at development environment, not at CI
        maven {
            url "file:$localMavenRepo" // must be defined in gradle.properties
        }
    }
}

configurations {
	javaposProjects
	javaposSources
}

dependencies {
	javaposProjects ("org.javapos:javapos-contracts:$javaposContractsVersion") { transitive = false } 
	javaposProjects ("org.javapos:javapos-controls:$javaposControlsVersion") { transitive = false }
	javaposProjects ("org.javapos:javapos-config-loader:$jclVersion") { transitive = false }
	
	javaposSources ("org.javapos:javapos-contracts:$javaposContractsVersion:sources") { transitive = false } 
	javaposSources ("org.javapos:javapos-controls:$javaposControlsVersion:sources") { transitive = false }
	javaposSources ("org.javapos:javapos-config-loader:$jclVersion:sources") { transitive = false }
}


///////////////////////////////////////////////////////////////////////////////
// Build Tasks
///////////////////////////////////////////////////////////////////////////////

java {
    withSourcesJar()
    withJavadocJar()
}

def javaposManifest = 	manifest {
        attributes('Specification-Title': 'UnifiedPOS Standard',
				   'Specification-Vendor': 'UnifiedPOS Committee',
                   'Specification-Version': uposVersion,
                   'Implementation-Title': 'JavaPOS Library (Contracts, Controls, JCL)',
				   'Implementation-Vendor': 'github.com/JavaPOSWorkingGroup',
                   'Implementation-Version': version)
}

jar {
	baseName = artifactName
	manifest = javaposManifest

	inputs.property 'version', version
	inputs.property 'uposVersion', uposVersion
    
    duplicatesStrategy = DuplicatesStrategy.FAIL
	configurations.javaposProjects.each {
		from(zipTree(it)) {
			exclude 'META-INF/**'
		}
	}
}

sourcesJar {
	manifest = javaposManifest
    duplicatesStrategy = DuplicatesStrategy.FAIL
	configurations.javaposSources.each {
		from(zipTree(it)) {
			exclude 'META-INF/**'
		}
	}
}


///////////////////////////////////////////////////////////////////////////////
// Artifact Upload
///////////////////////////////////////////////////////////////////////////////

nexusPublishing {
    repositories {
        sonatype()
    }
}

def githubProjectUrl = 'https://github.com/JavaPOSWorkingGroup/javapos'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = artifactName
                description = 'JavaPOS Library'
                url = githubProjectUrl
                licenses {
                    license {
                        name = 'Common Public License (CPL) -- V1.0'
                        url = 'https://www.eclipse.org/legal/cpl-v10.html'
                    }
                }
                developers {
                    developer {
                        id = 'javapos'
                        name = 'JavaPOS Working Group'
                        email = 'builder@javapos.org'
                    }
                }
                scm {
                    connection = "scm:${githubProjectUrl}.git"
                    developerConnection = "scm:git:${githubProjectUrl}.git"
                    url = "${githubProjectUrl}.git"
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
